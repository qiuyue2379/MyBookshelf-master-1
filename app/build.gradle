apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'

static def releaseTime() {
    return new Date().format("yy.MMddHH", TimeZone.getTimeZone("GMT+8"))
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--all', '--count'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())+10000
    }
    catch (ignored) {
        return -1
    }
}

def name = "QIUYUE"
def version = "2." + releaseTime()
//def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

def keyFile = file('1.jks')

android {
    def currentVersionCode = getVersionCode()
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    signingConfigs {
        myConfig {
            storeFile keyFile
            storePassword "123456"
            keyAlias "key0"
            keyPassword "123456"
        }
    }

    defaultConfig {
        applicationId "com.gedoor.monkeybook.qiuyue"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode currentVersionCode
        versionName version
        project.ext.set("archivesBaseName", name + "_" + version)
        multiDexEnabled true

    }

    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            buildConfigField "boolean", "IS_RELEASE", "true"
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.myConfig
            applicationIdSuffix '.debug'
            versionNameSuffix ' debug'
            buildConfigField "boolean", "IS_RELEASE", "false"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "${name}_${defaultConfig.versionName}.apk"
            }
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

}

dependencies {
    api project(':basemvplib')
    testImplementation 'junit:junit:4.12'

    //fireBase
    //noinspection GradleDependency
    implementation 'com.google.firebase:firebase-core:16.0.1'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'

    //androidX
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.media:media:1.1.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //google
    implementation 'com.google.android.material:material:1.1.0-alpha02'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.google.android:flexbox:1.1.0'

    //GreenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.1.0'

    //codecMD5
    implementation 'commons-codec:commons-codec:1.11'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    //CircleImageView
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    //bind view
    implementation 'com.jakewharton:butterknife:9.0.0-rc3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc3'

    //动画
    implementation 'com.victor:lib:1.0.4'

    //二维码
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.6'

    //颜色选择
    implementation 'com.jaredrummler:colorpicker:1.0.5'

    //文件目录选择
    implementation 'com.github.gedoor.AndroidPicker:FilePicker:2.0.0'

    //简繁转换
    implementation 'com.luhuiguo:chinese-utils:1.0'

    //字符串比较
    implementation 'net.ricecode:string-similarity:1.0.0'

    //MarkDown
    implementation 'ru.noties:markwon:2.0.0'

    //epub
    implementation('nl.siegmann.epublib:epublib-core:3.1') {
        exclude group: 'xmlpull'
    }
}

greendao {
    schemaVersion 51
    daoPackage 'com.kunfei.bookshelf.dao'
    targetGenDir 'src/main/java'
}